Class {
	#name : #PCMouseEventTargetTest,
	#superclass : #TestCase,
	#instVars : [
		'space'
	],
	#category : #'BlocBenchs-PCRenderer-Tests'
}

{ #category : #tests }
PCMouseEventTargetTest >> assertMouseEventTargetAt: aPoint hasId: expectedId [

	self
		assert: (space root findMouseEventTargetAt: aPoint) id asSymbol
		equals: expectedId asSymbol
]

{ #category : #tests }
PCMouseEventTargetTest >> assertNoMouseEventTargetAt: aPoint [
	"Assert that the target at aPoint is the root, which means there are no elements there."

	self assertMouseEventTargetAt: aPoint hasId: space root id asSymbol
]

{ #category : #running }
PCMouseEventTargetTest >> setUp [
	super setUp.
	
	Bloc hasSparta: false.

]

{ #category : #running }
PCMouseEventTargetTest >> tearDown [
	
	Bloc hasSparta: true.
	super tearDown.

]

{ #category : #tests }
PCMouseEventTargetTest >> testCircles [

	space := BlSpaceFixture new spaceWithCircles.	

	self
		assertNoMouseEventTargetAt: 10 asPoint.

	self
		assertMouseEventTargetAt: 15 asPoint
		hasId: #A.

	self
		assertMouseEventTargetAt: 20 asPoint
		hasId: #B.

]

{ #category : #tests }
PCMouseEventTargetTest >> testNesting [

	space := BlSpaceFixture new spaceWithNesting.
	
	self
		assertMouseEventTargetAt: 10 asPoint
		hasId: #A.

	self
		assertMouseEventTargetAt: 15 asPoint
		hasId: #B.

	self
		assertMouseEventTargetAt: 20 asPoint
		hasId: #C. "elementA doesn't clip"

	"elementB DOES clip, then elementC is not found"
	self
		assertNoMouseEventTargetAt: 25 asPoint. 

]

{ #category : #tests }
PCMouseEventTargetTest >> testRectangles [

	space := BlSpaceFixture new spaceWithRectangles.
	
	"No element theer, only background."
	self
		assertNoMouseEventTargetAt: 9 asPoint. 

	self
		assertMouseEventTargetAt: 10 asPoint
		hasId: #A.

	self
		assertMouseEventTargetAt: 15 asPoint
		hasId: #B.

]

{ #category : #tests }
PCMouseEventTargetTest >> testTransformRotate [

	space := BlSpaceFixture new spaceWithTransformRotate.
	 
	{	 0 -> space root id.
		 5 -> #A.
		10 -> #A.
		15 -> #A.
		20 -> #A.
		25 -> #B.
		30 -> #C.
		35 -> #B.
		40 -> space root id } do: [ :each |
			self
				assertMouseEventTargetAt: each key asPoint
				hasId: each value ]

]

{ #category : #tests }
PCMouseEventTargetTest >> testTransformScale [

	space := BlSpaceFixture new spaceWithTransformScale.

	{ 	 5 -> space root id.
		10 -> #A.
		15 -> space root id.
		20 -> #B.
		25 -> #C.
		30 -> #B.
		35 -> space root id } do: [ :each |
			self
				assertMouseEventTargetAt: each key asPoint
				hasId: each value ]

]

{ #category : #tests }
PCMouseEventTargetTest >> testTransformTranslate [

	space := BlSpaceFixture new spaceWithTransformTranslate.
	
	self
		assertMouseEventTargetAt: 10 asPoint
		hasId: #A.

	self
		assertMouseEventTargetAt: 15 asPoint
		hasId: #B.

	self
		assertMouseEventTargetAt: 20 asPoint
		hasId: #C.

	self
		assertNoMouseEventTargetAt: 25 asPoint. 

]

{ #category : #tests }
PCMouseEventTargetTest >> testTriangles [

	space := BlSpaceFixture new spaceWithTriangles.

	self
		assertMouseEventTargetAt: 10 asPoint
		hasId: #A.

	self
		assertNoMouseEventTargetAt: 11@10.

	self
		assertMouseEventTargetAt: 15 asPoint
		hasId: #B.

]
